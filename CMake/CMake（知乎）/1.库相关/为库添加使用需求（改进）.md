使用需求（Usage requirements）让我们可以对库或可执行文件的链接和include进行更好的控制，同时也让我们对CMake中目标的可传递属性进行更多的控制。利用使用需求的主要命令是：

- `target_compile_definitions()`
- `target_compile_options()`
- `target_include_directories()`
- `target_link_libraries()`



```
.
├── CMakeLists.txt
├── MathFunctions
│   ├── CMakeLists.txt
│   ├── MathFunctions.h
│   └── mysqrt.cpp
├── TutorialConfig.h.in
└── tutorial.cpp
```

> 除CMakeLists.txt外，所有文件同 [添加库.md](添加库.md)



- MathFunctions/CMakeLists.txt：

  ```cmake
  add_library(MathFunctions mysqrt.cxx)
  
  # state that anybody linking to us needs to include the current source dir
  # to find MathFunctions.h, while we don't.
  target_include_directories(MathFunctions
            INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}
            )
  ```

  >任何链接到MathFunctions的人都需要包含当前的源目录，而MathFunctions本身不需要。所以这可以成为一个`INTERFACE`使用需求。

- CMakeLists.txt：

  ```cmake
  cmake_minimum_required(VERSION 3.10)
  
  # set the project name and version
  project(Tutorial VERSION 1.0)
  
  # specify the C++ standard
  set(CMAKE_CXX_STANDARD 11)
  set(CMAKE_CXX_STANDARD_REQUIRED True)
  
  # should we use our own math functions
  option(USE_MYMATH "Use tutorial provided math implementation" ON)
  
  # configure a header file to pass some of the CMake settings
  # to the source code
  configure_file(TutorialConfig.h.in TutorialConfig.h)
  
  # add the MathFunctions library
  if(USE_MYMATH)
    add_subdirectory(MathFunctions)
    list(APPEND EXTRA_LIBS MathFunctions)
  endif()
  
  # add the executable
  add_executable(Tutorial tutorial.cxx)
  
  target_link_libraries(Tutorial PUBLIC ${EXTRA_LIBS})
  
  # add the binary tree to the search path for include files
  # so that we will find TutorialConfig.h
  target_include_directories(Tutorial PUBLIC
                             "${PROJECT_BINARY_DIR}"
                             )
  ```



说明：

只要USE_MYMATH为true，就会执行if分支。

在执行add_subdirectory的时候，自动执行target_include_directories。

因此不需要在外面的CMakeLists.txt中再配置target_include_directories项。

